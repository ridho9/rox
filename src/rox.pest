program = { SOI ~ expr ~ EOI }

expr = { prefix? ~ primary ~ (infix ~ prefix? ~ primary)* }

infix = _{ add | sub | mul | div | eqeq | neq }
add   =  { "+" }
sub   =  { "-" }
mul   =  { "*" }
div   =  { "/" }
eqeq  =  { "==" }
neq   =  { "!=" }

prefix = _{ neg | not }
neg    =  { "-" }
not    =  { "!" }

primary    = _{ number | bool | nil | "(" ~ expr ~ ")" | string }
number     = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) ~ ("." ~ ASCII_DIGIT+)? }
bool       =  { "true" | "false" }
nil        =  { "nil" }
string     = ${ "\"" ~ inner ~ "\"" }
inner      = ${ char* }
char       =  { char_noesc | char_esc }
char_noesc =  { !("\"" | "\\") ~ ANY }
char_esc   =  {
    "\\" ~ ("\"" | "\\" | "/" | "n" | "r" | "t")
}

WHITESPACE = _{ " " | "\t" | NEWLINE }
