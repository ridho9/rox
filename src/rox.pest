program = { SOI ~ statements ~ EOI }

KEYWORD = { "let" | "print" | "true" | "false" | "nil" }

statements      = { statement* }
statement       = { print_statement | let_statement }
let_statement   = { "let" ~ ident ~ ("=" ~ expr)? ~ ";" }
print_statement = { "print" ~ expr ~ ";" }

expr = { prefix? ~ primary ~ (infix ~ prefix? ~ primary)* }

infix = _{ add | sub | mul | div | eqeq | neq | le | lt | ge | gt }
add   =  { "+" }
sub   =  { "-" }
mul   =  { "*" }
div   =  { "/" }
eqeq  =  { "==" }
neq   =  { "!=" }
le    =  { "<=" }
lt    =  { "<" }
ge    =  { ">=" }
gt    =  { ">" }

prefix = _{ neg | not }
neg    =  { "-" }
not    =  { "!" }

primary    = _{ number | bool | nil | "(" ~ expr ~ ")" | string | ident }
number     = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) ~ ("." ~ ASCII_DIGIT+)? }
bool       =  { "true" | "false" }
nil        =  { "nil" }
string     = ${ "\"" ~ inner ~ "\"" }
inner      = ${ char* }
char       =  { char_noesc | char_esc }
char_noesc =  { !("\"" | "\\") ~ ANY }
char_esc   =  {
    "\\" ~ ("\"" | "\\" | "/" | "n" | "r" | "t")
}
ident      = @{ !(KEYWORD ~ !ASCII_ALPHA) ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" | NEWLINE }
